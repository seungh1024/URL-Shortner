plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'checkstyle'
    id 'jacoco'
    id 'org.sonarqube' version '7.2.2.6593'
    id 'com.google.protobuf' version '0.9.4'
}

group = 'com.shortener'
version = '0.0.1-SNAPSHOT'
description = 'URL_Shortener'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'

    // TSID
    implementation 'com.github.f4b6a3:tsid-creator:5.2.6'

    // gRPC - 버전 통일 (3.1.0.RELEASE가 사용하는 1.63.0으로)
    implementation 'net.devh:grpc-server-spring-boot-starter:3.1.0.RELEASE'

    // Java 9+ 호환성
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    // 테스트 의존성
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-test-autoconfigure'
    testImplementation 'org.springframework.boot:spring-boot-webmvc-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // gRPC 클라이언트 테스트용
    testImplementation 'net.devh:grpc-client-spring-boot-starter:3.1.0.RELEASE'

    // test container
    testImplementation 'org.testcontainers:testcontainers:1.19.4'
    testImplementation 'org.testcontainers:junit-jupiter:1.19.4'
    testImplementation 'org.testcontainers:mysql:1.19.4'
}

// Protobuf 플러그인 설정
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.1"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.63.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

// 생성된 코드 경로
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

checkstyle {
    configFile = file("${rootDir}/naver-checkstyle.xml")
    configProperties = ["suppressionFile": "${rootDir}/naver-checkstyle-suppressions.xml"]
    toolVersion = "9.2"
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

sonar {
    properties {
        property "sonar.projectKey", "seungh1024_URL-Shortner"
        property "sonar.organization", "seungh1024"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.java.checkstyle.reportPaths", "build/reports/checkstyle/main.xml"
    }
}
